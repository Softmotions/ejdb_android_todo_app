apply plugin: "com.android.application"

def abiName = "armeabi-v7a"

android {
  compileSdkVersion 25
  defaultConfig {
    applicationId "com.softmotions.apptodolist"
    minSdkVersion 21
    targetSdkVersion 25
    versionCode 1
    versionName '1.0'
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    externalNativeBuild {
      cmake {
        abiFilters abiName
        targets "ejdb2jni"
        arguments "-DCMAKE_MAKE_PROGRAM=/usr/bin/ninja",
                  "-DCMAKE_BUILD_TYPE=debug",
                  "-DBUILD_JNI_BINDING=ON",
                  "-DENABLE_HTTP=OFF",
                  "-DBUILD_EXAMPLES=OFF",
                  "-DCMAKE_TOOLCHAIN_FILE=$ndkDirectory/build/cmake/android.toolchain.cmake",
                  "-DANDROID_ABI=$abiName",
                  "-DANDROID_PLATFORM=android-21",
                  "-DANDROID_NATIVE_API_LEVEL=24"
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
  externalNativeBuild {
    cmake {
      path "../../ejdb/CMakeLists.txt"
    }
  }
}

project.afterEvaluate {
  if (tasks.findByName("externalNativeBuildDebug")) {
    javaPreCompileDebug.dependsOn externalNativeBuildDebug
    externalNativeBuildDebug.doLast {
      copy {
        from(".externalNativeBuild/cmake/debug/$abiName/src/bindings/ejdb2_jni/src") {
          include "ejdb2jni.jar"
        }
        into "libs"
      }
    }
  } else if (tasks.findByName("externalNativeBuildRelease")) {
    javaPreCompileRelease.dependsOn externalNativeBuildRelease
    externalNativeBuildRelease.doLast {
      copy {
        from(".externalNativeBuild/cmake/release/$abiName/src/bindings/ejdb2_jni/src") {
          include "ejdb2jni.jar"
        }
        into "libs"
      }
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation "com.android.support:appcompat-v7:25.1.1"
  implementation "com.android.support:design:25.1.1"
  implementation "com.android.support:recyclerview-v7:25.1.1"
  implementation "com.android.support:cardview-v7:25.1.1"
  implementation "com.android.support.constraint:constraint-layout:1.0.0-beta4"

  implementation "com.mobsandgeeks:android-saripaar:2.0.3"
  implementation "com.jakewharton:butterknife:8.5.1"
  annotationProcessor "com.jakewharton:butterknife-compiler:8.5.1"

  testImplementation "junit:junit:4.12"
  androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
    exclude group: "com.android.support', module: 'support-annotations"
  })

  implementation "com.fasterxml.jackson.core:jackson-core:2.9.8"
  implementation "com.fasterxml.jackson.core:jackson-annotations:2.9.8"
  implementation "com.fasterxml.jackson.core:jackson-databind:2.9.8"
}
